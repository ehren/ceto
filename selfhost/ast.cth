# vim: syntax=python

include <map>
include <typeinfo>

include (visitor)
include (utility)
include (range_utility)


class (Source:
    source = ""s
)

struct (SourceLoc:
    source: Source
    loc: int

    def (init, source: Source = None, loc: int = 0:
        self.source = source
        self.loc = loc
    )

    header_file_cth = ""s
    header_file_h = ""s
)

class (Scope)

class (Node:
    func: Node
    args: [Node]
    source: SourceLoc

    def (init, func, args, source = SourceLoc():
        self.func = func
        self.args = args
        self.source = source
    )

    declared_type: Node = None
    scope: Scope = None
    _parent: Node:weak = {}

    def (classname: virtual:
        return ceto.util.typeid_name(*this)
    ) : std.string

    def (repr: virtual:
        classname = self.classname()
        csv = ceto.util.join(self.args, lambda(a, a.repr()), ", ")
        return classname + "(" + self.func.repr() + ")([" + csv + "])"
    ) : std.string

    def (name: virtual:
        return std.nullopt
    ) : std.optional<std.string>

    def (accept: virtual, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )
    
    def (cloned_args:
        new_args: mut:[Node] = []
        new_args.reserve(self.args.size())
        for (a in self.args:
            new_args.append(a.clone())
        )
        return new_args
    ) : [Node]

    def (clone: virtual:
        none: Node:mut = None
        c: mut = Node(if (self.func: self.func.clone() else: none), self.cloned_args(), self.source)
        return c
    ) : Node:mut

    def (equals: virtual, other: Node:
        if (other == None:
            return False
        )

        if (typeid(*this) != typeid(*other):
            return False
        )

        if (self.func and not self.func.equals(other.func):
            return False
        elif not self.func and other.func:
            return False
        )

        if (self.args.size() != other.args.size():
            return False
        )

        for (i in ceto.util.range(self.args.size()):
            if (not self.args[i].equals(other.args[i]):
                return False
            )
        )

        return True
    ) : bool

    def (parent:
        return self._parent.lock()
    )

    def (set_parent: mut, p: Node:
        self._parent = p
    )

    def (destruct: virtual:
        pass
    )
)

class (UnOp(Node):
    op : std.string

    def (init, op, args:[Node], source = SourceLoc():
        self.op = op
        super.init(None, args, source)
    )

    def (repr: override:
        return "("s +  self.op + " " + self.args[0].repr() + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = UnOp(self.op, self.cloned_args(), source)
        return c
    ) : Node:mut
)

class (LeftAssociativeUnOp(Node):
    op : std.string

    def (init, op, args:[Node], source = SourceLoc():
        self.op = op
        super.init(None, args, source)
    )

    def (repr: override:
        return "(" + self.args[0].repr() + " " + self.op + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = LeftAssociativeUnOp(self.op, self.cloned_args(), source)
        return c
    ) : Node:mut
)

class (BinOp(Node):
    op : std.string

    def (init, op, args:[Node], source = SourceLoc():
        self.op = op
        super.init(None, args, source)
    )

    def (lhs:
        return self.args[0]
    )

    def (rhs:
        return self.args[1]
    )

    def (repr: override:
        return "(" + self.lhs().repr() + " " + self.op + " " + self.rhs().repr() + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = BinOp(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut

    def (equals: override, other_node: Node:
        other = asinstance(other_node, BinOp)

        if (not other:
            return False
        )

        if (self.op != other.op:
            return False
        )

        return self.lhs().equals(other.lhs()) and self.rhs().equals(other.rhs())
    ) : bool
)

class (TypeOp(BinOp):

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = TypeOp(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (SyntaxTypeOp(TypeOp):
    synthetic_lambda_return_lambda : Node = None

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = SyntaxTypeOp(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (AttributeAccess(BinOp):

    def (repr: override:
        return self.lhs().repr() + "." + self.rhs().repr()
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = AttributeAccess(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (ArrowOp(BinOp):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = ArrowOp(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (ScopeResolution(BinOp):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = ScopeResolution(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (Assign(BinOp):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = Assign(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (NamedParameter(Assign):
    def (repr: override:
        return "NamedParameter("s + ceto.util.join(self.args, lambda(a, a.repr()), ", ")  + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = NamedParameter(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (BitwiseOrOp(BinOp):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = BitwiseOrOp(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (EqualsCompareOp(BinOp):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = EqualsCompareOp(self.op, self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (Identifier(Node):
    _name : string

    def (init, name, source = SourceLoc():
        self._name = name
        super.init(None, [] : Node, source)
    )

    def (repr: override:
        return self._name
    ) : std.string

    def (name: override:
        return self._name
    ) : std.optional<std.string>

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (equals: override, other_node: Node:
        other = asinstance(other_node, Identifier)
        if (not other:
            return False
        )
        return self._name == other._name
    ) : bool

    def (clone: override:
        c: mut = std.make_shared<Identifier.class>(*this)
        return c
    ) : Node:mut
)

class (Call(Node):
    is_one_liner_if = false

    def (repr: override:
        csv = ceto.util.join(self.args, lambda (a, a.repr()), ", ")
        return self.func.repr() + "(" + csv + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = Call(self.func.clone(), self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (ArrayAccess(Node):
    def (repr: override:
        csv = ceto.util.join(self.args, lambda (a, a.repr()), ", ")
        return self.func.repr() + "[" + csv + "]"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = ArrayAccess(self.func.clone(), self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (BracedCall(Node):
    def (repr: override:
        csv = ceto.util.join(self.args, lambda (a, a.repr()), ", ")
        return self.func.repr() + "{" + csv + "}"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = BracedCall(self.func.clone(), self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (Template(Node):
    def (repr: override:
        csv = ceto.util.join(self.args, lambda (a, a.repr()), ", ")
        return self.func.repr() + "<" + csv + ">"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = Template(self.func.clone(), self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (StringLiteral(Node):
    str : string
    prefix : Identifier
    suffix : Identifier

    def (init, str, prefix: Identifier = None, suffix: Identifier = None, source = SourceLoc():
        self.str = str
        self.prefix = prefix
        self.suffix = suffix
        super.init(None, [] : Node, source)
    )

    def (escaped:
        s: mut = ceto.util.string_replace(self.str, "\\", "\\\\")
        s = ceto.util.string_replace(s, "\n", "\\n")
        s = ceto.util.string_replace(s, '"', '\\"')
        s = '"'s + s + '"'
        return s
    )

    def (repr: override:
        return if (self.prefix:
            self.prefix.name().value()
        else:
            ""s
        ) + self.escaped() + if (self.suffix:
            self.suffix.name().value()
        else:
            ""s
        )
    ) : string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (equals: override, other_node: Node:
        other = asinstance(other_node, StringLiteral)
        if (not other:
            return False
        )
        if (self.str != other.str:
            return False
        )
        if (self.prefix and not self.prefix.equals(other.prefix):
            return False
        elif not self.prefix and other.prefix:
            return False
        )
        if (self.suffix and not self.suffix.equals(other.suffix):
            return False
        elif not self.suffix and other.suffix:
            return False
        )
        return True
    ) : bool

    def (clone: override:
        c: mut = StringLiteral(self.str, 
            if (self.prefix: asinstance(self.prefix.clone(), Identifier) else: self.prefix),
            if (self.suffix: asinstance(self.suffix.clone(), Identifier) else: self.suffix),
            self.source)
        return c
    ) : Node:mut
)

class (IntegerLiteral(Node):
    integer_string : std.string
    suffix : Identifier

    def (init, integer_string, suffix: Identifier = None, source = SourceLoc():
        self.integer_string = integer_string
        self.suffix = suffix
        super.init(None, {}, source)
    )

    def (repr: override:
        return self.integer_string + if (self.suffix: self.suffix.name().value() else: ""s)
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = IntegerLiteral(self.integer_string,
            if (self.suffix: asinstance(self.suffix.clone(), Identifier) else: self.suffix),
            self.source)
        return c
    ) : Node:mut

    def (equals: override, other_node: Node:
        other = asinstance(other_node, IntegerLiteral)
        if (not other:
            return False
        )
        if (self.integer_string != other.integer_string:
            return False
        )
        if (self.suffix and not self.suffix.equals(other.suffix):
            return False
        elif not self.suffix and other.suffix:
            return False
        )
        return True
    ) : bool
)

class (FloatLiteral(Node):
    float_string : std.string
    suffix : Identifier

    def (init, float_string, suffix : Identifier, source = SourceLoc():
        self.float_string = float_string
        self.suffix = suffix
        super.init(None, {}, source)
    )

    def (repr: override:
        return self.float_string + if (self.suffix: self.suffix.name().value() else: ""s)
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (equals: override, other_node: Node:
        other = asinstance(other_node, FloatLiteral)
        if (not other:
            return False
        )
        if (self.float_string != other.float_string:
            return False
        )
        if (self.suffix and not self.suffix.equals(other.suffix):
            return False
        elif not self.suffix and other.suffix:
            return False
        )
        return True
    ) : bool

    def (clone: override:
        c: mut = FloatLiteral(self.float_string, 
            if (self.suffix: asinstance(self.suffix.clone(), Identifier) else: self.suffix), 
            self.source)
        return c
    ) : Node:mut
)

class (ListLike_(Node):
    def (init, args: [Node], source = SourceLoc():
        super.init(None, args, source)
    )

    def (repr: override:
        classname = self.classname()
        return classname + "(" + ceto.util.join(self.args, lambda (a, a.repr()), ", ") + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = ListLike_(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (ListLiteral(ListLike_):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = ListLiteral(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (TupleLiteral(ListLike_):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = TupleLiteral(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (BracedLiteral(ListLike_):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = BracedLiteral(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (Block(ListLike_):
    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = Block(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (Module(Block):
    has_main_function = false

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = Module(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (RedundantParens(Node):
    def (init, args: [Node], source = SourceLoc():
        super.init(None, args, source)
    )

    def (repr: override:
        classname = self.classname()
        return classname + "(" + ceto.util.join(self.args, lambda (a, a.repr()), ", ") + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = RedundantParens(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

class (InfixWrapper_(Node):
    def (init, args: [Node], source = SourceLoc():
        super.init(None, args, source)
    )

    def (repr: override:
        classname = self.classname()
        return classname + "(" + ceto.util.join(self.args, lambda (a, a.repr()), ", ") + ")"
    ) : std.string

    def (accept: override, visitor: Visitor:mut:ref:
        visitor.visit(*this)
    )

    def (clone: override:
        c: mut = InfixWrapper_(self.cloned_args(), self.source)
        return c
    ) : Node:mut
)

def (gensym:
    counter: mut:static:unsigned:long:long = 0
    s = Identifier("ceto__private__ident__" + std.to_string(counter))
    counter += 1
    return s
)

#no:
#defmacro (wild(x) : std.Function = lambda(wild(b)), x, b:
#)
# this might work:
#defmacro (x: std.function = lambda(b), x: Identifier, b:  # b is generic so instance of WildCard. x is a WildCard with stored_type == Integer
#    # should allow either/mix of these
#    return Assign([TypeOp([x, Call(Identifier("decltype"), [b.parent])]), b.parent])
#    return quote(unquote(x) : std.function(decltype(lambda(unquote(b)))) = lambda(unquote(b)))
#)
#
# 'pattern' is unnecessary:
#defmacro (pattern(x: std.function, t) = pattern(lambda(b), l), x: Identifier, b: Node:
#defmacro (x: std.function = pattern(lambda(Wild(b)), l), x: Identifier, l: Call:
#    return quote(unquote(
#)
#

#defmacro (x: std.function = lambda(b...), x : Identifier, b:  # b is generic but it's using in a ... expression so it's a vector of WildCard. x is a WildInteger or maybe just WildCard with stored_type == Integer
#    return quote(unquote(x) : std.function(decltype(lambda(unquote(b)))) = lambda(unquote(b)))  # should unquote auto unpack a vector of Nodes?
#    return quote(unquote(x) : std.function(decltype(lambda(unpack(b)))) = lambda(unpack(b)))  # maybe unpack is clearer or at least easier to implement
#    return quote(unquote(x) : std.function(decltype(lambda(unquote(b...)))) = lambda(unquote(b...)))  # more clever if a bit more confusing and C++y
#)
# ^ although this would preclude a macro that modifies ... expressions? alternative:
#defmacro (x: std.function = lambda(b), x : Identifier, b:  # b is generic but it's fed to 'unpack' so it's a vector of WildCard. x is a WildInteger or maybe just WildCard with stored_type == Integer
#    return quote(unquote(x) : std.function(decltype(lambda(unpack(b)))) = lambda(unpack(b)))
#)
# better:
#defmacro (x: std.function = lambda(b), x: Identifier, b: ...:  # b is a vector of WildCard. x is a WildInteger or maybe just WildCard with stored_type == Integer
#    return quote(unquote(x) : std.function(decltype(lambda(unpack(b)))) = lambda(unpack(b)))  # unpack distinct from unquote is better in case we can't determine at transpile time if b is a vector
#)

# see a macro
# defmacro_node = ...
# compile macro_impl in dll
# MacroDefinition(pattern=defmacro_node.args[0], action=macro_impl)
#
#def matches(x, y) -> :
#    if x == y == nullptr:
#        return true, None
#     if isinstance(y, WildCard):
#        if not y.stored_typeid or y.stored_typeid() == typeid(x)
#            return true, {y : x}
#        else:
#            return false, None
#    if typeid(x) != typeid(y):    # ugly
#        return false, None
#    if y._is_wildcard:  # ugly (especially in combination with typdid)
#        return true # or the match? {y : x}
#    if len(x.args) != len(y.args):
#        return false, None
#    if len(x.args) == 0 and x.func == None:
#        return x.repr() == y.repr(), None  # ugly
#    submatches = {}
#    for i in range(len(x.args)):
#        m = matches(x[i], y[i])
#         if not m:
#           return false, None
#         submatches.extend(m)
#     m = matches(x.func, y.func):
#     if not m:
#        return false, None
#    submatches.extend(m)
#    return true, submatches
#
#
#       
#
# expansion:
# have a node
# for pattern, action in macro_definitions:
#     if match_dict := matches(node, pattern):
#         node = macro_trampoline(action, match_dict)
#         #node = action(match_dict)
#
# def (macro_action1, match_dict:
#    x = match_dict["x"]
#    y = match_dict["y"]
#
#    return ...
# )
#
# def (macro_trampoline, action_ptr, match_dict:
#     return *action_ptr(match_dict)
#)

# this should probably take an index into an already dlsymed table of fptrs
#def (macro_trampoline, fptr : uintptr_t, matches: std.map<string, Node>:
#    # writing a wrapper type for pybind11 around the correct function pointer would be better (fine for now)
#    # note that extra parens required around '+' to specify lambda return type (precedence of ':' vs '+') TODO(?) remove the dubious post-parse hacks for immediatelly invoked lambdas with a return type (just require parens)
#    f = reinterpret_cast<decltype(+(lambda(matches:std.map<string, Node>, None): Node))>(fptr)  # no explicit function ptr syntax yet/ever(?)
#    return (*f)(matches)
#)
