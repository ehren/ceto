include <map>

include (ast)
include (utility)

unsafe()

class (ClassDefinition:
    name_node : Identifier
    class_def_node : Call
    is_unique: bool
    is_struct: bool
    is_forward_declaration: bool

    is_pure_virtual = false
    is_concrete = false
#    is_generic_param_index = std.map<int, bool>()
#
#    def (has_generic_params:
#        for ((k, v) in self.is_generic_param_index:
#            if (v:
#                return true
#            )
#        )
#
#        return false
#    )

    def (repr:
        return self.class_name() + "(" + self.name_node.repr() + ", " + self.class_def_node.repr() + std.to_string(self.is_unique) + ", " + std.to_string(self.is_struct) + ", " + std.to_string(self.is_forward_declaration) + ")"
    )

    def (class_name: virtual:
        return ceto.util.typeid_name(*this)
    ) : std.string

    def (destruct: virtual:
        pass
    )
)

# TODO we should just remove this and the built-in codegen "interface" feature
# (user code can just use abstract base classes/pure virtual functions)
class (InterfaceDefinition(ClassDefinition):
    def (init:
        super.init(None, None, false, false, false)
    )
)

class (FunctionDefinition:
    def_node: Node
    function_name: Identifier

    def (class_name: virtual:
        return ceto.util.typeid_name(*this)
    ) : std.string

    def (repr:
        return self.class_name() + "(" + self.def_node.repr() + ", " + self.function_name.repr() + ")"
    )

    def (destruct: virtual:
        pass
    )
)

class (NamespaceDefinition:
    namespace_node: Call
    namespace_name: Node

    def (class_name: virtual:
        return ceto.util.typeid_name(*this)
    ) : std.string

    def (repr:
        return self.class_name() + "(" + self.namespace_node.repr() + ", " + self.namespace_name.repr() + ")"
    )

    def (destruct: virtual:
        pass
    )
)

class (VariableDefinition:
    defined_node: Identifier
    defining_node: Node

    def (class_name: virtual:
        return ceto.util.typeid_name(*this)
    ) : std.string

    def (repr:
        return self.class_name() + "(" + self.defined_node.repr() + ", " + self.defining_node.repr() + ")"
    )

    def (destruct: virtual:
        pass
    )
)

class (LocalVariableDefinition(VariableDefinition):
    pass
)

class (GlobalVariableDefinition(VariableDefinition):
    pass
)

class (FieldDefinition(VariableDefinition):
    pass
)

class (ParameterDefinition(VariableDefinition):
    pass
)

def (creates_new_variable_scope, e: Node:
    if (isinstance(e, Call):
        name = e.func.name()
        if (name:
            return ceto.util.contains(["def"s, "lambda"s, "class"s, "struct"s, "defmacro"s], name.value())
        elif isinstance(e.func, ArrayAccess) and e.func.func.name() == "lambda":
            return True
        )
    )
    return False
)


def (comes_before, root: Node, before: Node, after: Node:
    if (root == before:
        return true
    elif root == after:
        return false
    )
    for (arg in root.args:
        cb = comes_before(arg, before, after)
        if (cb.has_value():
            return cb
        )
    )
    if (root.func:
        cb = comes_before(root.func, before, after)
        if (cb.has_value():
            return cb
        )
    )
    return {}
) : std.optional<bool>


class (Scope:
    interfaces = std.map<std.string, std.vector<Node>>()
    class_definitions: [ClassDefinition] = []
    variable_definitions: [VariableDefinition] = []
    function_definitions: [FunctionDefinition] = []
    namespace_definitions: [NamespaceDefinition] = []

    indent = 0
    _parent: weak:Scope = {}
    in_function_body = False
    in_function_param_list = False  # TODO unused remove
    in_class_body = False
    in_decltype = False
    is_unsafe = False

    def (indent_str:
        return std.string(4*self.indent, char' ')
    )

    def (add_variable_definition: mut, defined_node: Identifier, defining_node: Node:
        parent: mut = defined_node.parent()

        while (parent:
            if (creates_new_variable_scope(parent):
                name = parent.func.name()
                if (name == "class" or name == "struct":
                    defn = FieldDefinition(defined_node, defining_node)
                    self.variable_definitions.push_back(defn)
                elif name == "def" or name == "lambda" or name == "defmacro":
                    defn = ParameterDefinition(defined_node, defining_node)
                    self.variable_definitions.push_back(defn)
                else:
                    defn = LocalVariableDefinition(defined_node, defining_node)
                    self.variable_definitions.push_back(defn)
                    std.cerr << "this is no good?\n"
                )
                return
            elif isinstance(parent, Block) and creates_new_variable_scope(parent.parent()):
                name = parent.parent().func.name()

                if (name == "class" or name == "struct":
                    defn = FieldDefinition(defined_node, defining_node)
                    self.variable_definitions.push_back(defn)
                else:
                    defn = LocalVariableDefinition(defined_node, defining_node)
                    self.variable_definitions.push_back(defn)
                )
                return
            )
            parent = parent.parent()
        )
        defn = GlobalVariableDefinition(defined_node, defining_node)
        self.variable_definitions.push_back(defn)
    )

    def (add_interface_method: mut, interface_name: std.string, interface_method_def_node: Node:
        self.interfaces[interface_name].push_back(interface_method_def_node)
    )

    def (add_class_definition: mut, class_definition: ClassDefinition:
        self.class_definitions.push_back(class_definition)
    )

    def (add_function_definition: mut, function_definition: FunctionDefinition:
        self.function_definitions.push_back(function_definition)
    )

    def (add_namespace_definition: mut, namespace_definition: NamespaceDefinition:
        self.namespace_definitions.push_back(namespace_definition)
    )

    def (lookup_class, class_node: Node:
        if (not isinstance(class_node, Identifier):
            return None
        )

        for (c in self.class_definitions:
            if (c.name_node.name() == class_node.name():
                return c
            )
        )
        if (self.interfaces.contains(class_node.name().value()):
            return InterfaceDefinition()
        )
        if ((s = self._parent.lock()):
            return s.lookup_class(class_node)
        )
        return None
    ) : ClassDefinition

    def (lookup_function, function_name_node: Node:
        if (not isinstance(function_name_node, Identifier):
            return None
        )

        for (f in self.function_definitions:
            if (f.function_name.name() == function_name_node.name():
                return f
            )
        )
        if ((s = self._parent.lock()):
            return s.lookup_function(function_name_node)
        )
        return None
    ) : FunctionDefinition

    def (lookup_namespace, namespace_name_node: Node:
        if (not isinstance(namespace_name_node, Identifier):
            return None
        )

        for (n in self.namespace_definitions:
            if (n.namespace_name.name() == namespace_name_node.name():
                return n
            )
        )
        if ((s = self._parent.lock()):
            return s.lookup_namespace(namespace_name_node)
        )
        return None
    ) : NamespaceDefinition

    def (find_defs, var_node: Node, find_all=true:

        if (not isinstance(var_node, Identifier):
            return {}
        )

        # results: mut = []  # untyped lists (implemented using range_value_t) not supported in clang 14/15
        results: mut:[VariableDefinition] = []

        for (d in self.variable_definitions:
            if (d.defined_node.name() == var_node.name() and d.defined_node != var_node:
                # macro expansion (as currently implemented) breaks this:
                #defined_loc = std.get<1>(d.defined_node.source.source)
                #var_loc = std.get<1>(var_node.source.loc)

                # we should just compute a 'node_loc' in a previous pass
                parent_block: mut = d.defined_node.parent()
                while (True:
                    if (isinstance(parent_block, Module):
                        break
                    )
                    parent_block = parent_block.parent()
                )

                defined_before = comes_before(parent_block, d.defined_node, var_node)

#                if (defined_loc < var_loc:
                if (defined_before and defined_before.value():
                    if (not find_all:
                        return [d]
                    )
                    results.append(d)
                    if ((assign = asinstance(d.defining_node, Assign)):
                        if ((ident = asinstance(assign.rhs(), Identifier)):
                            more = self.find_defs(ident, find_all)
                            results.insert(results.end(), more.begin(), more.end())
                        )
                    )
                )
            )
        )

        if ((s = self._parent.lock()):
            more = s.find_defs(var_node, find_all)
            results.insert(results.end(), more.begin(), more.end())
        )

        return results
    ) : [VariableDefinition]

    def (find_def, var_node: Node:
        find_all = false
        found = self.find_defs(var_node, find_all)
        return if (found.size() > 0:
            found[0]
        else:
            none_result : VariableDefinition = None
            none_result  # needed for lambda return type deduction; arguably expression-if usage here is silly.
        )
    )

    def (enter_scope:
        s: mut = Scope()
        s._parent = ceto.get_underlying(self)
        s.in_function_body = self.in_function_body
        s.in_decltype = self.in_decltype
        s.is_unsafe = self.is_unsafe
        s.indent = self.indent + 1
        return s
    ) : Scope

    def (parent:
        return self._parent.lock()
    )
)
